
#import "Basic";

Asn1_Tag :: enum u8 {
    INTEGER            :: 0x02;
    BIT_STRING         :: 0x03;
    OCTET_STRING       :: 0x04;
    NULL               :: 0x05;
    OBJECT_IDENTIFIER  :: 0x06;
    PRINTABLE_STRING   :: 0x13;
    UTC_TIME           :: 0x17;
    GENERALIZED_TIME   :: 0x18;
    SEQUENCE           :: 0x30;
    SET                :: 0x31;
    CONTEXT_0          :: 0xa0;
}

Asn1_Parser :: struct {
    using root: Construction;

    construction_stack: [..] Construction;

    Construction :: struct {
        data: []u8;
        cursor: int;
    }

    error: bool;
}

get_current_construction :: (parser: Asn1_Parser) -> *Asn1_Parser.Construction {
    if parser.construction_stack {
        return *parser.construction_stack[parser.construction_stack.count-1];
    }

    return *parser.root;
}

peek_tag :: (parser: Asn1_Parser) -> Asn1_Tag {
    if parser.error return 0;

    using get_current_construction(parser);

    // if cursor >= data.count return 0xff;
    return cast(Asn1_Tag) data[cursor];
}

parse_error :: () #expand {
    `parser.error = true;
    `return 0;
}

read_length :: (parser: *Asn1_Parser) -> int {
    if parser.error return 0;

    using get_current_construction(parser);

    if cursor >= data.count {
        parse_error();
    }

    first := data[cursor];
    cursor += 1;

    if (first & 0x80) == 0 {
        if cursor + first > data.count {
            parse_error();
        }

        return first;
    }

    octets := first & 0x7f;
    if octets == 0 || octets > 4 {
        parse_error();
    }

    if cursor + octets > data.count {
        parse_error();
    }

    length: int;
    for 0..octets-1 {
        length = (length << 8) | data[cursor + it];
    }

    cursor += octets;
    return length;

    // Todo error: return parser.cursor + length.* <= parser.data.count;
}

read_value :: (parser: *Asn1_Parser, expected_tag: Asn1_Tag) -> [] u8 {
    if parser.error return .{};

    using get_current_construction(parser);

    // Todo: if parser.cursor >= parser.data.count return false;

    tag := cast(Asn1_Tag)data[cursor];
    cursor += 1;

    assert(tag == expected_tag); // Todo

    length := read_length(parser);

    result := []u8.{length, data.data + cursor};
    cursor += length;

    return result;
}

read_bit_string :: (parser: *Asn1_Parser, bytes: *[]u8, unused_bits: *u8) -> bool {
    if parser.error return false;

    raw := read_value(parser, .BIT_STRING);
    if raw.count == 0 return false;

    unused_bits.* = raw.data.*;

    if raw.count == 1 {
        bytes.* = .{};
        return true;
    }

    bytes.* = .{raw.count - 1, raw.data + 1};
    return true;
}



asn1_construction :: (parser: *Asn1_Parser, tag: Asn1_Tag, call := #caller_code) #expand {
    container := read_value(parser, tag);

    const := array_add(*parser.construction_stack);
    const.data = container;

    #insert,scope(call) #code {
        `it := null;
        `it_index := 0;
    };

    `defer pop(*parser.construction_stack);
}

asn1_sequence :: (parser: *Asn1_Parser, body: Code, flags: For_Flags) #expand {
    asn1_construction(parser, .SEQUENCE);
    #insert body;
}

asn1_context :: (parser: *Asn1_Parser, body: Code, flags: For_Flags) #expand {
    asn1_construction(parser, .CONTEXT_0);
    #insert body;
}



X509_Certificate :: struct {
    version: int;
    serial_number: []u8;

    tbs_signature_algorithm_oid: []u8;
    issuer: []u8;

    issuer_country: string;
    issuer_organizational_unit: string;

    not_valid_before: string;
    not_valid_after: string;
    subject: []u8;

    subject_public_key_algorithm_oid: []u8;
    subject_public_key: []u8;
    subject_public_key_unused_bits: u8;

    signature_algorithm_oid: []u8;
    signature_value: []u8;
    signature_unused_bits: u8;
}

parse_x509_certificate :: (certificate: []u8) -> bool, X509_Certificate {
    parser := Asn1_Parser.{data=certificate};
    cert: X509_Certificate;

    for :asn1_sequence parser {
        for :asn1_sequence parser {
            cert.version = 1;

            for :asn1_context parser {
                version_value := read_value(*parser, .INTEGER);
                cert.version = version_value[version_value.count - 1].(int) + 1;
            }

            cert.serial_number = read_value(*parser, .INTEGER);

            for :asn1_sequence parser {
                cert.tbs_signature_algorithm_oid = read_value(*parser, .OBJECT_IDENTIFIER);
            }

            cert.issuer = read_value(*parser, .SEQUENCE);

            for :asn1_sequence parser {
                tag := peek_tag(parser);
                // Todo: if tag != Asn1_Tag.UTC_TIME && tag != Asn1_Tag.GENERALIZED_TIME return false;
                cert.not_valid_before = cast(string) read_value(*parser, tag);

                tag = peek_tag(parser);
                // Todo: if tag != Asn1_Tag.UTC_TIME && tag != Asn1_Tag.GENERALIZED_TIME return false;
                cert.not_valid_after = cast(string) read_value(*parser, tag);
            }

            cert.subject = read_value(*parser, .SEQUENCE);

            for :asn1_sequence parser {
                for :asn1_sequence parser {
                    cert.subject_public_key_algorithm_oid = read_value(*parser, .OBJECT_IDENTIFIER);
                }

                read_bit_string(*parser, *cert.subject_public_key, *cert.subject_public_key_unused_bits);
            }
        }

        // Todo: additional optional IDs and extensions.

        for :asn1_sequence parser {
            cert.signature_algorithm_oid = read_value(*parser, .OBJECT_IDENTIFIER);
        }

        read_bit_string(*parser, *cert.signature_value, *cert.signature_unused_bits);
    }

    return !parser.error, cert;
}











#run {
    #import "String";
    #import "Compiler";

    cl := get_toplevel_command_line();

    // Auto run the test without generating an executable, if the module gets passed to the compiler directly on the command line.
    if cl.count == 2 && contains(cl[1], "ASN_1.jai") {
        set_build_options_dc(.{do_output = false});

        result, cert := parse_x509_certificate(test_cert);
        assert(result);

        context.print_style.default_format_struct.use_newlines_if_long_form = true;
        context.print_style.default_format_int.base = 16;
        log("%\n", cert);
    }
}

test_cert :: u8.[
    0x30, 0x82, 0x03, 0x21, 0x30, 0x82, 0x02, 0x09, 0xa0, 0x03, 0x02, 0x01, 0x02, 0x02, 0x08, 0x15,
    0x5a, 0x92, 0xad, 0xc2, 0x04, 0x8f, 0x90, 0x30, 0x0d, 0x06, 0x09, 0x2a, 0x86, 0x48, 0x86, 0xf7,
    0x0d, 0x01, 0x01, 0x0b, 0x05, 0x00, 0x30, 0x22, 0x31, 0x0b, 0x30, 0x09, 0x06, 0x03, 0x55, 0x04,
    0x06, 0x13, 0x02, 0x55, 0x53, 0x31, 0x13, 0x30, 0x11, 0x06, 0x03, 0x55, 0x04, 0x0a, 0x13, 0x0a,
    0x45, 0x78, 0x61, 0x6d, 0x70, 0x6c, 0x65, 0x20, 0x43, 0x41, 0x30, 0x1e, 0x17, 0x0d, 0x31, 0x38,
    0x31, 0x30, 0x30, 0x35, 0x30, 0x31, 0x33, 0x38, 0x31, 0x37, 0x5a, 0x17, 0x0d, 0x31, 0x39, 0x31,
    0x30, 0x30, 0x35, 0x30, 0x31, 0x33, 0x38, 0x31, 0x37, 0x5a, 0x30, 0x2b, 0x31, 0x0b, 0x30, 0x09,
    0x06, 0x03, 0x55, 0x04, 0x06, 0x13, 0x02, 0x55, 0x53, 0x31, 0x1c, 0x30, 0x1a, 0x06, 0x03, 0x55,
    0x04, 0x03, 0x13, 0x13, 0x65, 0x78, 0x61, 0x6d, 0x70, 0x6c, 0x65, 0x2e, 0x75, 0x6c, 0x66, 0x68,
    0x65, 0x69, 0x6d, 0x2e, 0x6e, 0x65, 0x74, 0x30, 0x82, 0x01, 0x22, 0x30, 0x0d, 0x06, 0x09, 0x2a,
    0x86, 0x48, 0x86, 0xf7, 0x0d, 0x01, 0x01, 0x01, 0x05, 0x00, 0x03, 0x82, 0x01, 0x0f, 0x00, 0x30,
    0x82, 0x01, 0x0a, 0x02, 0x82, 0x01, 0x01, 0x00, 0xc4, 0x80, 0x36, 0x06, 0xba, 0xe7, 0x47, 0x6b,
    0x08, 0x94, 0x04, 0xec, 0xa7, 0xb6, 0x91, 0x04, 0x3f, 0xf7, 0x92, 0xbc, 0x19, 0xee, 0xfb, 0x7d,
    0x74, 0xd7, 0xa8, 0x0d, 0x00, 0x1e, 0x7b, 0x4b, 0x3a, 0x4a, 0xe6, 0x0f, 0xe8, 0xc0, 0x71, 0xfc,
    0x73, 0xe7, 0x02, 0x4c, 0x0d, 0xbc, 0xf4, 0xbd, 0xd1, 0x1d, 0x39, 0x6b, 0xba, 0x70, 0x46, 0x4a,
    0x13, 0xe9, 0x4a, 0xf8, 0x3d, 0xf3, 0xe1, 0x09, 0x59, 0x54, 0x7b, 0xc9, 0x55, 0xfb, 0x41, 0x2d,
    0xa3, 0x76, 0x52, 0x11, 0xe1, 0xf3, 0xdc, 0x77, 0x6c, 0xaa, 0x53, 0x37, 0x6e, 0xca, 0x3a, 0xec,
    0xbe, 0xc3, 0xaa, 0xb7, 0x3b, 0x31, 0xd5, 0x6c, 0xb6, 0x52, 0x9c, 0x80, 0x98, 0xbc, 0xc9, 0xe0,
    0x28, 0x18, 0xe2, 0x0b, 0xf7, 0xf8, 0xa0, 0x3a, 0xfd, 0x17, 0x04, 0x50, 0x9e, 0xce, 0x79, 0xbd,
    0x9f, 0x39, 0xf1, 0xea, 0x69, 0xec, 0x47, 0x97, 0x2e, 0x83, 0x0f, 0xb5, 0xca, 0x95, 0xde, 0x95,
    0xa1, 0xe6, 0x04, 0x22, 0xd5, 0xee, 0xbe, 0x52, 0x79, 0x54, 0xa1, 0xe7, 0xbf, 0x8a, 0x86, 0xf6,
    0x46, 0x6d, 0x0d, 0x9f, 0x16, 0x95, 0x1a, 0x4c, 0xf7, 0xa0, 0x46, 0x92, 0x59, 0x5c, 0x13, 0x52,
    0xf2, 0x54, 0x9e, 0x5a, 0xfb, 0x4e, 0xbf, 0xd7, 0x7a, 0x37, 0x95, 0x01, 0x44, 0xe4, 0xc0, 0x26,
    0x87, 0x4c, 0x65, 0x3e, 0x40, 0x7d, 0x7d, 0x23, 0x07, 0x44, 0x01, 0xf4, 0x84, 0xff, 0xd0, 0x8f,
    0x7a, 0x1f, 0xa0, 0x52, 0x10, 0xd1, 0xf4, 0xf0, 0xd5, 0xce, 0x79, 0x70, 0x29, 0x32, 0xe2, 0xca,
    0xbe, 0x70, 0x1f, 0xdf, 0xad, 0x6b, 0x4b, 0xb7, 0x11, 0x01, 0xf4, 0x4b, 0xad, 0x66, 0x6a, 0x11,
    0x13, 0x0f, 0xe2, 0xee, 0x82, 0x9e, 0x4d, 0x02, 0x9d, 0xc9, 0x1c, 0xdd, 0x67, 0x16, 0xdb, 0xb9,
    0x06, 0x18, 0x86, 0xed, 0xc1, 0xba, 0x94, 0x21, 0x02, 0x03, 0x01, 0x00, 0x01, 0xa3, 0x52, 0x30,
    0x50, 0x30, 0x0e, 0x06, 0x03, 0x55, 0x1d, 0x0f, 0x01, 0x01, 0xff, 0x04, 0x04, 0x03, 0x02, 0x05,
    0xa0, 0x30, 0x1d, 0x06, 0x03, 0x55, 0x1d, 0x25, 0x04, 0x16, 0x30, 0x14, 0x06, 0x08, 0x2b, 0x06,
    0x01, 0x05, 0x05, 0x07, 0x03, 0x02, 0x06, 0x08, 0x2b, 0x06, 0x01, 0x05, 0x05, 0x07, 0x03, 0x01,
    0x30, 0x1f, 0x06, 0x03, 0x55, 0x1d, 0x23, 0x04, 0x18, 0x30, 0x16, 0x80, 0x14, 0x89, 0x4f, 0xde,
    0x5b, 0xcc, 0x69, 0xe2, 0x52, 0xcf, 0x3e, 0xa3, 0x00, 0xdf, 0xb1, 0x97, 0xb8, 0x1d, 0xe1, 0xc1,
    0x46, 0x30, 0x0d, 0x06, 0x09, 0x2a, 0x86, 0x48, 0x86, 0xf7, 0x0d, 0x01, 0x01, 0x0b, 0x05, 0x00,
    0x03, 0x82, 0x01, 0x01, 0x00, 0x59, 0x16, 0x45, 0xa6, 0x9a, 0x2e, 0x37, 0x79, 0xe4, 0xf6, 0xdd,
    0x27, 0x1a, 0xba, 0x1c, 0x0b, 0xfd, 0x6c, 0xd7, 0x55, 0x99, 0xb5, 0xe7, 0xc3, 0x6e, 0x53, 0x3e,
    0xff, 0x36, 0x59, 0x08, 0x43, 0x24, 0xc9, 0xe7, 0xa5, 0x04, 0x07, 0x9d, 0x39, 0xe0, 0xd4, 0x29,
    0x87, 0xff, 0xe3, 0xeb, 0xdd, 0x09, 0xc1, 0xcf, 0x1d, 0x91, 0x44, 0x55, 0x87, 0x0b, 0x57, 0x1d,
    0xd1, 0x9b, 0xdf, 0x1d, 0x24, 0xf8, 0xbb, 0x9a, 0x11, 0xfe, 0x80, 0xfd, 0x59, 0x2b, 0xa0, 0x39,
    0x8c, 0xde, 0x11, 0xe2, 0x65, 0x1e, 0x61, 0x8c, 0xe5, 0x98, 0xfa, 0x96, 0xe5, 0x37, 0x2e, 0xef,
    0x3d, 0x24, 0x8a, 0xfd, 0xe1, 0x74, 0x63, 0xeb, 0xbf, 0xab, 0xb8, 0xe4, 0xd1, 0xab, 0x50, 0x2a,
    0x54, 0xec, 0x00, 0x64, 0xe9, 0x2f, 0x78, 0x19, 0x66, 0x0d, 0x3f, 0x27, 0xcf, 0x20, 0x9e, 0x66,
    0x7f, 0xce, 0x5a, 0xe2, 0xe4, 0xac, 0x99, 0xc7, 0xc9, 0x38, 0x18, 0xf8, 0xb2, 0x51, 0x07, 0x22,
    0xdf, 0xed, 0x97, 0xf3, 0x2e, 0x3e, 0x93, 0x49, 0xd4, 0xc6, 0x6c, 0x9e, 0xa6, 0x39, 0x6d, 0x74,
    0x44, 0x62, 0xa0, 0x6b, 0x42, 0xc6, 0xd5, 0xba, 0x68, 0x8e, 0xac, 0x3a, 0x01, 0x7b, 0xdd, 0xfc,
    0x8e, 0x2c, 0xfc, 0xad, 0x27, 0xcb, 0x69, 0xd3, 0xcc, 0xdc, 0xa2, 0x80, 0x41, 0x44, 0x65, 0xd3,
    0xae, 0x34, 0x8c, 0xe0, 0xf3, 0x4a, 0xb2, 0xfb, 0x9c, 0x61, 0x83, 0x71, 0x31, 0x2b, 0x19, 0x10,
    0x41, 0x64, 0x1c, 0x23, 0x7f, 0x11, 0xa5, 0xd6, 0x5c, 0x84, 0x4f, 0x04, 0x04, 0x84, 0x99, 0x38,
    0x71, 0x2b, 0x95, 0x9e, 0xd6, 0x85, 0xbc, 0x5c, 0x5d, 0xd6, 0x45, 0xed, 0x19, 0x90, 0x94, 0x73,
    0x40, 0x29, 0x26, 0xdc, 0xb4, 0x0e, 0x34, 0x69, 0xa1, 0x59, 0x41, 0xe8, 0xe2, 0xcc, 0xa8, 0x4b,
    0xb6, 0x08, 0x46, 0x36, 0xa0
];
